### A Pluto.jl notebook ###
# v0.17.0

using Markdown
using InteractiveUtils

# â•”â•â•¡ 3e9d08f8-3ea1-11ec-262e-bb3d43960aec
md"""# Simple Two Dimensional Flow Fields

Simulate trajectories of a particle cloud in a simple, two-dimensional, doubly-periodic, flow field. 

For additional documentation e.g. see :
[1](https://JuliaClimate.github.io/IndividualDisplacements.jl/dev/),
[2](https://JuliaClimate.github.io/MeshArrays.jl/dev/),
[3](https://docs.juliadiffeq.org/latest/solvers/ode_solve.html),
[4](https://en.wikipedia.org/wiki/Displacement_(vector))
"""

# â•”â•â•¡ 192fc454-054c-4364-a9ed-1aa4969b612a
begin
	using IndividualDisplacements, DataFrames, MeshArrays
	using Plots, ColorSchemes, PlutoUI
	
	"done with loading packages"
end

# â•”â•â•¡ bb710e3c-9f38-4feb-a241-f624d2fca943
TableOfContents()

# â•”â•â•¡ 023222b6-e2ac-43cd-be10-f5d9b7ce0124
md"""## Define A Flow Field

Let's start with a simple, doubly periodic flow field defined by a streamfunction
and create the `FlowFields` data structure which will then be drive the 
individual displacement and trajectory computations.
"""

# â•”â•â•¡ a2c57844-080e-4598-bef5-4d5dfb740a63
begin
	nx=16
	dx= Ï€/nx
	XC = dx*(collect(1:2*nx) .- 0.5)
	YC = dx*(collect(1:nx) .- 0.5)
	
	fac=0.1
	f(x, y) = sin(x) + cos(y) #streamfunction
	Ï• = fac*[f(x, y) for x in XC,y in YC] #streamfunction
	uC = -fac*[sin(y) for x in XC,y in YC] #dphi/dy at cell center
	vC = -fac*[cos(x) for x in XC,y in YC] #-dphi/dx at cell center
	"done with defining flow field at grid cell centers"
end

# â•”â•â•¡ 96b1d76e-1563-4f90-b821-feb75baea771
md"""
When starting with collocated velocities in m/s units (`uC,vC` at the grid cell center), one easily obtains the normalized, staggered C-grid velocities (`u,v`). The resulting  `u` (resp `v`) is staggered by `-0.5` grid point in direction `1` for `u` (`2` for `v`) relative to `uC,vC`. This staggering of variables and the normalization of velocities by corresponding grid scales (`dx` below) are  conventions that apply thrhougout `IndividualDisplacements.jl`.

These flowfields, for consecutive time steps, are then embedded in a `FlowFields` data structure, along with time range `(t0,t1)`. Between `t0` and `t1` velcocities are interpolated linearly going from `u0,v0` to `u1,v1`. By setting `u1=u0=u` and `v1=v0=v`, the flow field remains the same throughout `(t0,t1)`.
"""

# â•”â•â•¡ aa86042f-3da6-4252-9493-9a713688b4b1
begin
	u=0.5*(circshift(uC, (1,0))+uC) /dx #staggered u converted to grid point units (m/s -> 1/s)
	v=0.5*(circshift(vC, (0,1))+vC) /dx #staggered v converted to grid point units (m/s -> 1/s)
	ğ‘‡=(0.,10.)
	ğ¹=FlowFields(u,u,v,v,ğ‘‡)
	"done with staggered flow field definition"
end

# â•”â•â•¡ 47c87570-ca36-468f-9b9d-5c41de490105
md"""## Initialize Individuals

Here we initialize 100 particles within a subdomainand wraps everything in the `Individuals` data structure.
"""

# â•”â•â•¡ 53a79184-9a72-47a5-a6be-fc3c5fe7a096
begin
	np,nq=size(u)
	x=np*(0.4 .+ 0.2*rand(100))
	y=nq*(0.4 .+ 0.2*rand(100))
	ğ¼=Individuals(ğ¹,x,y)
end

# â•”â•â•¡ dffe1032-a247-4008-be22-692abcbe458a
md"""## Compute Trajectories

The time period is `ğ¼.ğ‘ƒ.ğ‘‡` by default, unless `âˆ«!(ğ¼,ğ‘‡)` is called instead as done below. 

Note that the size of ğŸ”´ is different from before -- this DataFrame is a record of the trajectories.
"""

# â•”â•â•¡ 28a3af5d-c1b3-4d95-8d06-034e1ad4f585
begin
	âˆ«!(ğ¼,ğ‘‡)
	ğ¼
end

# â•”â•â•¡ 6a6c82d8-b8d3-4a94-af6e-79039ceaa157
md"""## Plot Results"""

# â•”â•â•¡ 9288b04f-0f38-4d8a-9af1-027147799079
function plot(Ï•,df)
    nx,ny=size(Ï•)
	if isa(df,GroupedDataFrame)
	    f=contour(-0.5 .+ (1:nx),-0.5 .+ (1:ny), transpose(Ï•), c = :black, colorbar=:none,
			linewidth = 0.5, xlims=(0,nx),ylims=(0,ny))	
		co=cgrad(:buda,1:length(df))
		for i in 1:length(df)
			scatter!(df[i].x,df[i].y,markersize=3.0, palette=co,
				marker = (:circle, stroke(0)), leg=:none)
		end
	else
	    f=contourf(-0.5 .+ (1:nx),-0.5 .+ (1:ny), transpose(Ï•), c = :blues, colorbar=:none,
			linewidth = 0.5, xlims=(0,nx),ylims=(0,ny))	
	    scatter!(df.x,df.y,markersize=4.0,c=:red,marker = (:circle, stroke(0)),leg=:none)
	end
	f
end

# â•”â•â•¡ 0366f590-e132-4a7f-877b-2168566faf60
begin	
	ğŸ”´_by_t = groupby(ğ¼.ğŸ”´, :t)
	plot(Ï•,ğŸ”´_by_t[end])
end

# â•”â•â•¡ 4a5d4cf9-faa3-45e1-85fa-50132f4836ec
plot(Ï•,ğŸ”´_by_t[1:100:end])
#size(ğŸ”´_by_t[1:1000:end])

# â•”â•â•¡ c0f0c2c6-7d74-4e2a-87f7-d96971e234de
md""" To generate a simple animation, try this for example:

```
anim = @animate for t in 1:10:length(ğŸ”´_by_t)
   plot(Ï•,ğŸ”´_by_t[t])
end

gif_file=joinpath(tempdir(),"RandomFlow.gif")
gif(anim, gif_file, fps = 20)
```
"""

# â•”â•â•¡ eb979c4c-549e-4912-aac8-e816c93017a1
begin
	anim = @animate for t in 1:10:length(ğŸ”´_by_t)
	   plot(Ï•,ğŸ”´_by_t[t])
	end
	
	gif_file=joinpath(tempdir(),"RandomFlow.gif")
	gif(anim, gif_file, fps = 20)
end

# â•”â•â•¡ 67c39f1e-8aa5-4fad-a0b8-a7fca1b17c36
md"""## Exercises

- change the initial distribution of particles
- increase the duration of the trajectories simulation
- treat the non-periodic domain case by padding `u,v` with zeros 
- make the flow field time variable `ğ¹=FlowFields(-u,u,-v,v,(0.,10.))`
- replace `u,v` with your own two-dimensional flow fields 
"""

# â•”â•â•¡ feb874c4-80dd-444f-beb5-90b90647d44d
md"""## Extras

Instead of using the common `Array` type it can be advantageous to use [MeshArrays.jl](https://juliaclimate.github.io/MeshArrays.jl/dev/) which provides functionalities for staggered vector fields and gridded domain decomposition. The `convert_to_FlowFields` convenience function does the conversion for you. The only other difference from the `Array` case is the need to provide a vector of subdomain indices to `Individuals`. Here this is just a vector of ones since `convert_to_FlowFields` does not decompose the gridded domain.

```
ğ¹=convert_to_FlowFields(u,v,10.0)
ğ¼=Individuals(ğ¹,x,y,fill(1,length(x)))
```

The `random_flow_field` function, found below, provides generates random flow fields that can be used instead of the analytical formulation used above. The "Rotational Component" option is most similar to what done in the original example.

```
(U,V,Î¦)=random_flow_field("Rotational Component";np=2*nx,nq=nx)
F=convert_to_FlowFields(U,V,10.0)
I=Individuals(F,x,y,fill(1,length(x)))
```

The other option, "Divergent Component", generates a purely divergent flow field instead. Try it and should you notice a qualitatively different outcome in terms of trajectories.

In general, user defined `uC, vC` fields may have both rotational and divergent components. [MeshArrays.jl](https://juliaclimate.github.io/MeshArrays.jl/dev/) provides an implementation of the [Helmholtz decomposition](https://en.wikipedia.org/wiki/Helmholtz_decomposition) to separate them out as sometimes needed.
"""

# â•”â•â•¡ e927ba74-2c88-493e-b25b-910e23a63045
begin
	(U,V,Î¦)=random_flow_field("Rotational Component";np=2*nx,nq=nx)
	F=convert_to_FlowFields(U,V,10.0)
	I=Individuals(F,x,y,fill(1,length(x)))
	âˆ«!(I)
	I
end

# â•”â•â•¡ ef8cbf8e-3e10-4615-9640-86cb8bc68288
function random_flow_field(option::String;np=12,nq=18)

	#define gridded domain
	Î“=simple_periodic_domain(np,nq)
	Î³=Î“.XC.grid
	Î“=UnitGrid(Î³;option="full")

    #initialize 2D field of random numbers
    tmp1=randn(Float64,Tuple(Î³.ioSize))
    Ï•=Î³.read(tmp1,MeshArray(Î³,Float64))

    #apply smoother
    Ï•=smooth(Ï•,3*Î“.DXC,3*Î“.DYC,Î“);

	#derive flow field
	if option=="Divergent Component"
		#For the convergent / scalar potential case, Ï• is interpreted as being 
		#on center points -- hence the standard gradient function readily gives 
		#what we need
		(u,v)=gradient(Ï•,Î“) 
		tmp=(u[1],v[1],Ï•[1])
	elseif option=="Rotational Component"
		#For the rotational / streamfunction case, Ï• is interpreted as being 
		#on S/W corner points -- this is ok here since the grid is homegeneous, 
		#and conveniently yields an adequate substitution u,v <- -v,u; but note
		#that doing the same with gradient() would shift indices inconsistenly
		u=-(circshift(Ï•[1], (0,-1))-Ï•[1])
		v=(circshift(Ï•[1], (-1,0))-Ï•[1])
		tmp=(u,v,Ï•[1])
	else
		error("non-recognized option")
	end
	return tmp[1],tmp[2],tmp[3]
end

# â•”â•â•¡ Cell order:
# â•Ÿâ”€3e9d08f8-3ea1-11ec-262e-bb3d43960aec
# â•Ÿâ”€192fc454-054c-4364-a9ed-1aa4969b612a
# â•Ÿâ”€bb710e3c-9f38-4feb-a241-f624d2fca943
# â•Ÿâ”€023222b6-e2ac-43cd-be10-f5d9b7ce0124
# â• â•a2c57844-080e-4598-bef5-4d5dfb740a63
# â•Ÿâ”€96b1d76e-1563-4f90-b821-feb75baea771
# â• â•aa86042f-3da6-4252-9493-9a713688b4b1
# â•Ÿâ”€47c87570-ca36-468f-9b9d-5c41de490105
# â• â•53a79184-9a72-47a5-a6be-fc3c5fe7a096
# â•Ÿâ”€dffe1032-a247-4008-be22-692abcbe458a
# â• â•28a3af5d-c1b3-4d95-8d06-034e1ad4f585
# â•Ÿâ”€6a6c82d8-b8d3-4a94-af6e-79039ceaa157
# â•Ÿâ”€9288b04f-0f38-4d8a-9af1-027147799079
# â•Ÿâ”€0366f590-e132-4a7f-877b-2168566faf60
# â•Ÿâ”€4a5d4cf9-faa3-45e1-85fa-50132f4836ec
# â•Ÿâ”€c0f0c2c6-7d74-4e2a-87f7-d96971e234de
# â•Ÿâ”€eb979c4c-549e-4912-aac8-e816c93017a1
# â•Ÿâ”€67c39f1e-8aa5-4fad-a0b8-a7fca1b17c36
# â•Ÿâ”€feb874c4-80dd-444f-beb5-90b90647d44d
# â•Ÿâ”€e927ba74-2c88-493e-b25b-910e23a63045
# â• â•ef8cbf8e-3e10-4615-9640-86cb8bc68288
